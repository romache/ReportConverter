<?xml version="1.0" encoding="utf-8"?>
<s:WindowedApplication xmlns:fx="http://ns.adobe.com/mxml/2009" 
					   xmlns:s="library://ns.adobe.com/flex/spark" 
					   xmlns:mx="library://ns.adobe.com/flex/mx"
					   xmlns:rc="reportconverter.*"
					   title="Time Report Converter"
					   enabled="{col.loaded}"
					   creationComplete="outPath = guessOutPath(inPath); output = createFile(outPath, onOutFileSelect);">
	<s:layout>
		<s:VerticalLayout verticalAlign="middle" horizontalAlign="center"/>
	</s:layout>
	
	<s:Form width="100%">
		<s:FormItem label="Report(s)">
			<s:HGroup width="100%">
				<s:TextInput id="inName" width="100%" editable="false" text="{inPath}"/>
				<s:Button label="..." width="35"
						  click="input.browseForDirectory('Select report or directory with reports')"/>
			</s:HGroup>
			<s:CheckBox id="recursive" label="and subfolders"/>
		</s:FormItem>
		<s:FormItem label="Output file">
			<s:layout>
				<s:HorizontalLayout/>
			</s:layout>
			<s:TextInput id="outName" width="100%" editable="false" text="{outPath}"/>
			<s:Button label="..." width="35"
					  click="output.browseForSave('Save to')"/>
		</s:FormItem>
	</s:Form>
	<s:Button label="GO"
			  click="convert()"/>
	
	<s:Label text="Left to load: {col.toLoad}"
			 visible="{!col.loaded}"/>

	<fx:Declarations>
		<rc:FileReadCollection id="col" complete="onCollectionLoadComplete(event)">
			<rc:sort>
				<s:Sort>
					<s:fields>
						<s:SortField name="date"/>
					</s:fields>
				</s:Sort>
			</rc:sort>
		</rc:FileReadCollection>
	</fx:Declarations>
	<fx:Script>
		<![CDATA[
			import com.as3xls.xls.ExcelFile;
			import com.as3xls.xls.Sheet;
			
			import mx.collections.IList;
			import mx.controls.Alert;
			import mx.core.EventPriority;
			import mx.events.CloseEvent;
			
			import reportconverter.Done;
			import reportconverter.Report;
			
			import spark.formatters.DateTimeFormatter;
			
			[Bindable]
			public var inPath:String = File.userDirectory.resolvePath("Dropbox/farata/reports").nativePath;
			private var input:File = createFile(inPath, onInFileSelect);
			[Bindable]
			public var outPath:String;
			private var output:File;
			
			private function buildOutPath(date:Date):String {
				var f:DateTimeFormatter = new DateTimeFormatter();
				f.dateTimePattern = "yyyy.MM";
				return File.userDirectory.resolvePath("Downloads/timesheet " + f.format(date) + "g.xls").nativePath;
			}
			
			private function createFile(path:String, handler:Function):File {
				var file:File = new File(path);
				file.addEventListener(Event.SELECT, handler, false, EventPriority.DEFAULT, true);
				return file;
			}
			
			private function onInFileSelect(event:Event):void {
				inPath = input.nativePath;
				if (outPath == buildOutPath(new Date())) {
					outPath = guessOutPath(inPath);
					output = createFile(outPath, onOutFileSelect);
				}
			}
			
			private function guessOutPath(inPath:String):String {
				var folder:String = inPath.substring(inPath.lastIndexOf("/")+1);
				var dateRe:RegExp = /(\b|\D)(\d{4})[-\._ ](\d{2})(\b|\D)/;
				var result:Object = dateRe.exec(folder);
				if (result) {
					var date:Date = new Date();
					date.fullYear = Number(result[2]);
					date.month = Number(result[3])-1;
					return buildOutPath(date);
				} else
					return buildOutPath(new Date());
			}
			
			private function onOutFileSelect(event:Event):void {
				outPath = output.nativePath;
			}
			
			private function read(file:File):FileReadCollection {
				col.removeAll();
				if (file.isDirectory)
					return readDir(file);
				else {
					var report:Report = readFile(file);
					if (report != null)
						col.addItem(report);
					return col;
				}
			}
			
			private function readDir(dir:File):FileReadCollection {
				var files:Array = dir.getDirectoryListing();
				for (var i:int = 0; i < files.length; i++) {
					var file:File = files[i] as File;
					if (!file.isDirectory) {
						var report:Report = readFile(file);
						if (report != null)
							col.addItem(report);
					} else if (recursive.selected) {
						col.addAll(readDir(file));
					}
				}
				return col;
			}
			
			private var re:RegExp = /^report (\d+)\D(\d+)\D(\d+)\.txt$/im;
			private function readFile(file:File):Report {
				var report:Report = null;
				var res:* = re.exec(file.name);
				if (res != null)
					report = new Report(new Date(res[1], Number(res[2]) - 1, res[3]), file);
				return report;
			}
			
			private function writeFile(bytes:ByteArray, file:File):File {
				var fileStream:FileStream = new FileStream();
				try {
					fileStream.open(file, FileMode.WRITE);
					fileStream.writeBytes(bytes);
				} catch (e:Error) {
					Alert.show("Couldn't write to " + file.nativePath, "Error writing");
				} finally {
					fileStream.close();
				}
				return file;
			}
			
			private function buildXLS(reports:IList):ByteArray {
//				var df:DateTimeFormatter = new DateTimeFormatter();
//				df.dateTimePattern="MM-dd-yyyy";
				var rows:Number = 0;
				for (var r:int = 0; r < reports.length; r++) {
					var report:Report = reports.getItemAt(r) as Report;
					report.filterDone();
					rows += report.lines.length;
				}
				
				var sheet:Sheet = new Sheet();
				var columns:Array = ["Day", "Task", "Hours", "Notes"];
				sheet.resize(rows + 1, columns.length);
				
				for (var colIndex:int = 0; colIndex < sheet.cols; colIndex++)
					sheet.setCell(0, colIndex, columns[colIndex]); 
				
				var i:int = 1;
				for (r = 0; r < reports.length; r++) {
					report = reports.getItemAt(r) as Report;
					sheet.setCell(i, 0, new Date(report.date.fullYear, report.date.month, report.date.date+1));//df.format(report.date));
					for (var l:int = 0; l < report.lines.length; l++) {
						var rl:Done = report.lines.getItemAt(l) as Done;
						sheet.setCell(i, 1, rl.lineExt);
						sheet.setCell(i, 2, rl.time);
						i++;
					}
				}
				
				for (r = 0; r < reports.length; r++) {
					report = reports.getItemAt(r) as Report;
					report.removeFilter();
				}
				
				var xls:ExcelFile = new ExcelFile();
				xls.sheets.addItem(sheet);
				return xls.saveToByteArray();
			}
			
			private function prepareFileName(name:String):String {
				var idx:Number = name.lastIndexOf(".");
				var ext:String = "";
				if (idx == -1 || name.substring(idx).toLowerCase().indexOf(".xls") == -1)
					ext = ".xls";
				return name + ext;
			}
			
			private function onCollectionLoadComplete(event:Event):void {
				writeFile(buildXLS(col), output);
				Alert.show("Should we open generated report?\nYou can find it here, btw:\n" + outPath,
					"Done!", Alert.YES | Alert.NO, null, doneHandler);
			}
			
			private function doneHandler(event:CloseEvent):void {
				if (event.detail == Alert.YES) {
					try {
						output.openWithDefaultApplication();
					} catch (e:Error) {
						trace(e.message);
						navigateToURL(new URLRequest(output.url));
					}
				}
			}
			
			private function convert():void {
				read(input);
				if (col.length == 0)
					Alert.show("Seems there's nothing to convert");
			}
			
		]]>
	</fx:Script>
</s:WindowedApplication>
